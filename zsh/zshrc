# Load dotfiles.env
[ -f "$HOME/.dotfiles.env" ] && source "$HOME/.dotfiles.env"

# Put your environment variables in ~/.localdotfile. It means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localdotfile ]]
then
  source ~/.localdotfile
fi

# Get all the .zshrc files and source them
for f in $DOTFILESD/**/*.zshrc(.); do source "$f"; done

# Add bin folder to PATH
export PATH="$DOTFILESD/bin:$PATH"

# tree but only print directories
# by default the depth level is 1 but an argument can be passed
# treed 3 - to have three levels of depth
treed() {
  tree -AdL ${1:-1}
}

# Create a new directory and enter it
mk() {
  mkdir -p "$@" && cd "$@"
}

# cd into latest created directory
# this is needed because I keep forgetting to use mk
cdl() {
  cd "$(ls -dt */ | head -n 1)"
}

# List only duplicate lines
duplines() {
  sort $1 | uniq -d
}

# List only unique lines
uniqlines() {
  sort $1 | uniq -u
}


# if condition is checking if homebrew is installed or not
# `brew shellenv`` prints a bunch of shell commands like export PATH=..., export HOMEBREW_PREFIX=..., etc.
# eval "$( ... )" executes those printed commands in your current shell.
if command -v brew >/dev/null; then
  eval "$(brew shellenv)"
fi

# load the compinit function when I first use it.
# -U avoids alias expansion; -z uses zsh-style autoloading.
autoload -Uz compinit
# compinit initializes zshâ€™s completion system (tab completion).
# It loads completion definitions so things like git <TAB> or brew <TAB> work nicely.
compinit


# Show system information
alias displays="system_profiler SPDisplaysDataType"
alias cpu="sysctl -n machdep.cpu.brand_string"
alias ram="top -l 1 -s 0 | grep PhysMem"
alias memory=ram

# Start screen saver
alias afk="open /System/Library/CoreServices/ScreenSaverEngine.app"

# Copy pwd to clipboard
pwdd(){ local dir="$PWD"; print -rn -- "$dir" | pbcopy; echo "copied: $dir to clipboard."; }

# force kill
fkill() {
  local pids
  pids=$(ps -eo pid=,command= | fzf -m --prompt='kill -9> ' --header='TAB to multi-select' | awk '{print $1}')
  [[ -n "$pids" ]] || return 0
  echo "$pids" | xargs kill -9
}

eval "$(rbenv init - zsh)"

export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion